apply plugin: 'java'
apply plugin: 'scala'

repositories {
    maven { url "http://repo.maven.apache.org/maven2" }
    maven { url "http://repo.hortonworks.com/content/groups/public/" }
    maven { url "http://repo.hortonworks.com/content/repositories/releases" }
    maven { url "http://conjars.org/repo" }
    maven { url "https://plugins.gradle.org/m2/" }
}


group = 'com.sparanagama'
version = '1.0.0-SNAPSHOT'

apply plugin: 'distribution'

configurations {
    all*.exclude group: "org.jboss.netty", module: "netty"
    compile.extendsFrom fat
}

def versions = [
        scala: "2.11",
        scala_lang: "2.11.2",
        scalding: "0.17.0",
        hadoop: "2.4.0.2.1.2.0-402"
]

dependencies {
    fat group: 'com.twitter', name: 'scalding-core_2.11', version: versions.scalding
    fat group: 'com.twitter', name: 'scalding-args_2.11', version: versions.scalding
    fat group: 'org.apache.hadoop', name: 'hadoop-common', version: versions.hadoop
    fat group: 'org.apache.hadoop', name: 'hadoop-mapreduce-client-core', version: versions.hadoop

    testCompile group: 'org.scalatest', name: 'scalatest_2.11', version: '3.0.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task fatjar(type: Jar) {

    manifest {
        attributes("Main-Class": "com.twitter.scalding.Tool")
    }
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'

    //baseName = project.name + '-fatjar'
    classifier = "job"

    zip64 true
    from {
        (configurations.fat).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    with jar
}

// tar.gz distribution to deploy on Azkaban host
distributions {
    main {
        contents {
            baseName = rootProject.name
            into( 'libs' ) {
                from new File("$buildDir/libs/")
            }
        }
    }
}
distTar {
    compression = Compression.GZIP
    extension = "tar.gz"
}

distTar.dependsOn([fatjar])

